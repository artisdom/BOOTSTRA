IF %MODE%==CLEAN GOTO :EOF

IF x%1==xFAR GOTO :XFAR
IF x%1==xNEAR GOTO :XNEAR
IF x%1==xLOCAL GOTO :XLOCAL
CALL ISMRM16 %1
IF %RESULT%==1 GOTO :INDIR
IF x%2==x GOTO :LOCAL
ECHO Expected local jump, got far (IP=%IP%)

:XLOCAL
SHIFT
:LOCAL
CALL RPN! _%IP%_2+        
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF

CALL RPN! %1_%IP%-
CALL IS8SIGN %RESULT%
IF %RESULT%==1 GOTO :NOWARN
ECHO Local jump offset truncated (IP=%IP%)
:NOWARN
CALL EMITBYTE 235
CALL RPN! _%1_%IP%-_256#
CALL EMITBYTE %RESULT%
GOTO :EOF

:XNEAR
SHIFT
CALL RPN! _%IP%_3+        
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL EMITBYTE 233
CALL RPN! _%1_%IP%-
CALL EMITWORD %RESULT%
GOTO :EOF

:XFAR
SHIFT
CALL ISMRM16 %1
IF %RESULT%==1 GOTO :FARINDIR
CALL RPN! _%IP%_5+        
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
CALL EMITBYTE 234
CALL EMITWORD %2
CALL EMITWORD %1
GOTO :EOF

:INDIR
CALL CMRM16 %1
CALL RPN! _%IP%_2+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :NOCHECK
CALL IS8SIGN %2
IF %RESULT%==1 GOTO :NOCHECK
ECHO Byte displacement truncated (IP=%IP%)
:NOCHECK
CALL EMITBYTE 255
CALL RPN! _%MRM%_32+
CALL EMITBYTE %RESULT%
IF %DISPBYTES%==1 CALL EMITBYTE %2
IF %DISPBYTES%==2 CALL EMITWORD %2
GOTO :EOF

:FARINDIR
CALL CMRM16 %1
CALL RPN! _%IP%_2+_%DISPBYTES%+
SET IP=%RESULT%
IF %MODE%==COUNT GOTO :EOF
IF NOT %DISPBYTES%==1 GOTO :FNOCHECK
CALL IS8SIGN %2
IF %RESULT%==1 GOTO :FNOCHECK
ECHO Byte displacement truncated (IP=%IP%)
:FNOCHECK
CALL EMITBYTE 255
CALL RPN! _%MRM%_40+
CALL EMITBYTE %RESULT%
IF %DISPBYTES%==1 CALL EMITBYTE %2
IF %DISPBYTES%==2 CALL EMITWORD %2

:EOF
